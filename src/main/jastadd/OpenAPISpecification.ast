//OpenAPI Object
OpenAPIObject ::= <OpenAPI> [InfoObject] ServerObject* PathsObject* [ComponentsObject] SecurityRequirementObject* TagObject* [ExternalDocObject] <Context:OAIContext> Extension*;

//Info Object
InfoObject ::= <Title> <Description> <TermsOfService> [ContactObject] [LicenseObject] <Version> Extension*;

//Contact Object
ContactObject ::= <Name> <Url> <Email> Extension*;

//License Object
LicenseObject ::= <Name> <Url>;

//Server Object
ServerObject ::= <Url> <Description> ServerVariablesTuple*;
ServerVariablesTuple ::= <Name> ServerVariableObject;

//Server Variable Object
ServerVariableObject ::= Enum* <Default> <Description>;
Enum ::= <EnumValue>;

//Components Object
ComponentsObject ::= SchemaTuple* ResponseTuple* ParameterTuple* ExampleTuple* RequestBodyTuple* HeaderTuple* SecuritySchemeTuple* LinkTuple* CallbackTuple* Extension*;
SchemaTuple ::= <Key> SchemaObject;
ResponseTuple ::= <Key> ResponseObject;
ParameterTuple ::= <Key> ParameterObject;
ExampleTuple ::= <Key> ExampleObject;
RequestBodyTuple ::= <Key> RequestBodyObject;
HeaderTuple ::= <Key> HeaderObject;
SecuritySchemeTuple ::= <Key> SecuritySchemeObject;
LinkTuple ::= <Key> LinkObject;
CallbackTuple ::= <Key> CallbackObject;


//Paths Object
PathsObject ::= <Ref> PathItem;

//Path Item Object
PathItem ::= <Ref> <Summary> <Description> [Get] [Put] [Post] [Delete] [Options] [Head] [Patch] [Trace] ServerObject* ParameterObject*;
Get ::= OperationObject;
Put ::= OperationObject;
Post ::= OperationObject;
Delete ::= OperationObject;
Options ::= OperationObject;
Head ::= OperationObject;
Patch ::= OperationObject;
Trace ::= OperationObject;

//Operation Object
OperationObject ::= Tag* <Summary> <Description> [ExternalDocObject] <OperationID> ParameterObject* [RequestBodyObject] ResponseTuple* CallbackTuple* <DeprecatedBoolean:Boolean> SecurityRequirementObject* ServerObject* <Required:Boolean> Extension*;
Tag ::= <Tag:String>;

//External Documentation Object
ExternalDocObject ::= <Description> <Url> Extension*;

//Parameter Object
ParameterObject ::= <Name> <In> <Description> <Required:Boolean> <DeprecatedBoolean:Boolean> <AllowEmptyValue:Boolean> <Style> <Explode:Boolean> <AllowReserved:Boolean> [SchemaObject] <Example:Object> ExampleTuple* ContentTuple* <Ref>;
ContentTuple ::= <Key> MediaTypeObject;

//Request Body Object
RequestBodyObject ::= <Description> ContentTuple* <Required:Boolean> <Ref>;

//Media Type Object
MediaTypeObject ::= [SchemaObject] <Example:Object> ExampleTuple* EncodingTuple*;
EncodingTuple ::= <Key> EncodingObject;

//Encoding Object
EncodingObject ::= <ContentType> HeaderTuple* <Style> <Explode:Boolean> <AllowReserved:Boolean> Extension*;

//Response Object
ResponseObject ::= <Ref> <Description> HeaderTuple* ContentTuple* LinkTuple*;

//Callback Object
CallbackObject ::= Expression* Extension* <Ref>;
Expression ::= <Name> PathItem;
Extension ::= <Key> <Value:Object>;

//Example Object
ExampleObject ::= <Summary> <Description> <Value:Object> <ExternalValue> Extension*;

//Link Object
LinkObject ::= <OperationRef> <OperationID> LinkParameterTuple* HeaderTuple* <Description> [ServerObject] <Ref>;
LinkParameterTuple ::= <LinkParameterKey> <LinkParameterValue>;

//Header Object
HeaderObject ::= <Description> <Required:Boolean> <DeprecatedBoolean:Boolean> <AllowEmptyValue:Boolean> <Style> <Explode:Boolean> <AllowReserved:Boolean> [SchemaObject] <Example:Object> ExampleTuple* ContentTuple* <Ref>;

//Tag Object
TagObject ::= <Name> <Description> [ExternalDocObject];

//Schema Object
SchemaObject ::= <AdditionalProperties:SchemaObject> <AdditionalPropertiesAllowed:Boolean> <DefaultValue:Object> <Description> <DeprecatedBoolean:Boolean> [DiscriminatorObject] EnumObj* <Example:Object> <ExclusiveMaximum:Boolean> <ExclusiveMinimum:Boolean> [ExternalDocObject] <Format> [ItemsSchema] <Maximum:Number> <Minimum:Number> <MaxItems:Integer> <MinItems:Integer> <MaxLength:Integer> <MinLength:Integer> <MaxProperties:Integer> <MinProperties:Integer> <MultipleOf:Number> [NotSchema] <Nullable:Boolean> <Pattern> PropertyItem* RequiredField* AllOfSchema* AnyOfSchema* OneOfSchema* <ReadOnly:Boolean> <WriteOnly:Boolean> <Type> <Title> <UniqueItems:Boolean> [XmlObject] <Ref> Extension*;
AdditionalProperties ::= SchemaObject;
EnumObj ::= <EnumOb:Object>;
ItemsSchema ::= SchemaObject;
NotSchema ::= SchemaObject;
PropertyItem ::= <Name> SchemaObject;
RequiredField ::= <Value>;
AllOfSchema ::= SchemaObject;
AnyOfSchema ::= SchemaObject;
OneOfSchema ::= SchemaObject;

//Discriminator Object
DiscriminatorObject ::= <PropertyName> MappingTuple*;
MappingTuple ::= <Key> <Value>;

//XML Object
XmlObject ::= <Name> <Namespace> <Prefix> <Attribute:Boolean> <Wrapped:Boolean>;

//Security Scheme Object
SecuritySchemeObject ::= <Type> <Description> <Name> <In> <Scheme> <BearerFormat> OAuthFlowsObject <OpenIdConnectUrl> <Ref> Extension*;

//OAuth Flows Object
OAuthFlowsObject ::= [Implicit] [Password] [ClientCredentials] [AuthorizationCode];
Implicit ::= OAuthFlowObject;
Password ::= OAuthFlowObject;
ClientCredentials ::= OAuthFlowObject;
AuthorizationCode ::= OAuthFlowObject;

//OAuth Flow Object
OAuthFlowObject ::= <AuthorizationUrl> <TokenUrl> <RefreshUrl> ScopesTuple* <Configuration>;
ScopesTuple ::= <ScopesKey> <ScopesValue>;

//Security Requirement Object
SecurityRequirementObject ::= SecurityRequirementTuple*;
SecurityRequirementTuple ::= <Name> SecurityRequirementValue*;
SecurityRequirementValue ::= <Value>;