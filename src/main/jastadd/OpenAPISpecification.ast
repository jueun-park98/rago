//OpenAPI Object
OpenAPIObject ::= <OpenAPI> [InfoObject] ServerObject* PathsObject* [ComponentsObject] SecurityRequirementObject* TagObject* [ExternalDocObject] <Context:OAIContext> Extension*;

//Info Object
InfoObject ::= <Title> <Description> <TermsOfService> [ContactObject] [LicenseObject] <Version> Extension*;

//Contact Object
ContactObject ::= <Name> <Url> <Email> Extension*;

//License Object
LicenseObject ::= <Name> <Url>;

//Server Object
ServerObject ::= <Url> <Description> ServerVariablesTuple*;
ServerVariablesTuple ::= <Name> ServerVariableObject;

//Server Variable Object
ServerVariableObject ::= Enum* <Default> <Description>;
Enum ::= <EnumValue>;

//Components Object
ComponentsObject ::= SchemaTuple* ResponseTuple* ParameterTuple* ExampleTuple* RequestBodyTuple* HeaderTuple* SecuritySchemeTuple* LinkTuple* CallbackTuple* Extension*;
SchemaTuple ::= <Key> SchemaOb;
ResponseTuple ::= <Key> ResponseOb;
ParameterTuple ::= <Key> ParameterOb;
ExampleTuple ::= <Key> ExampleOb;
RequestBodyTuple ::= <Key> RequestBodyOb;
HeaderTuple ::= <Key> HeaderOb;
SecuritySchemeTuple ::= <Key> SecuritySchemeOb;
LinkTuple ::= <Key> LinkOb;
CallbackTuple ::= <Key> CallbackOb;


//Paths Object
PathsObject ::= <Ref> PathItemOb;

//Path Item Object
abstract PathItemOb;
PathItemReference : PathItemOb ::= <Ref> <PathItemObject:PathItemObject>;
PathItemObject : PathItemOb ::= <Summary> <Description> [Get] [Put] [Post] [Delete] [Options] [Head] [Patch] [Trace] ServerObject* ParameterOb* Extension*;
Get ::= OperationObject;
Put ::= OperationObject;
Post ::= OperationObject;
Delete ::= OperationObject;
Options ::= OperationObject;
Head ::= OperationObject;
Patch ::= OperationObject;
Trace ::= OperationObject;

//Operation Object
OperationObject ::= Tag* <Summary> <Description> [ExternalDocObject] <OperationID> ParameterObject* [RequestBodyOb] ResponseTuple* CallbackTuple* <DeprecatedBoolean:Boolean> SecurityRequirementObject* ServerObject* <Required:Boolean> Extension*;
Tag ::= <Tag:String>;

//External Documentation Object
ExternalDocObject ::= <Description> <Url> Extension*;

//Parameter Object
abstract ParameterOb;
ParameterReference : ParameterOb ::= <Ref> <ParameterObject:ParameterObject>;
ParameterObject : ParameterOb ::= <Name> <In> <Description> <Required:Boolean> <DeprecatedBoolean:Boolean> <AllowEmptyValue:Boolean> <Style> <Explode:Boolean> <AllowReserved:Boolean> [SchemaOb] <Example:Object> ExampleTuple* ContentTuple* Extension*;
ContentTuple ::= <Key> MediaTypeObject;

//Request Body Object
abstract RequestBodyOb;
RequestBodyReference : RequestBodyOb ::= <Ref> <RequestBodyObject:RequestBodyObject>;
RequestBodyObject : RequestBodyOb ::= <Description> ContentTuple* <Required:Boolean>;

//Media Type Object
MediaTypeObject ::= [SchemaOb] <Example:Object> ExampleTuple* EncodingTuple*;
EncodingTuple ::= <Key> EncodingObject;

//Encoding Object
EncodingObject ::= <ContentType> HeaderTuple* <Style> <Explode:Boolean> <AllowReserved:Boolean> Extension*;

//Response Object
abstract ResponseOb;
ResponseReference : ResponseOb ::= <Ref> <ResponseObject:ResponseObject>;
ResponseObject : ResponseOb ::= <Description> HeaderTuple* ContentTuple* LinkTuple*;

//Callback Object
CallbackObject ::= Expression* Extension* <Ref>;
Expression ::= <Name> PathItemOb;
Extension ::= <Key> <Value:Object>;

//Example Object
abstract ExampleOb;
ExampleReference : ExampleOb ::= <Ref> <ExampleObject:ExampleObject>;
ExampleObject : ExampleOb ::= <Summary> <Description> <Value:Object> <ExternalValue> Extension*;

//Link Object
abstract LinkOb;
LinkReference : LinkOb ::= <Ref> <LinkObject:LinkObject>;
LinkObject ::= <OperationRef> <OperationID> LinkParameterTuple* HeaderTuple* <Description> [ServerObject];
LinkParameterTuple ::= <LinkParameterKey> <LinkParameterValue>;

//Header Object
abstract HeaderOb;
HeaderReference : HeaderOb ::= <Ref> <HeaderObject:HeaderObject>;
HeaderObject : HeaderOb ::= <Description> <Required:Boolean> <DeprecatedBoolean:Boolean> <AllowEmptyValue:Boolean> <Style> <Explode:Boolean> <AllowReserved:Boolean> [SchemaOb] <Example:Object> ExampleTuple* ContentTuple* <Ref>;

//Tag Object
TagObject ::= <Name> <Description> [ExternalDocObject] Extension*;

//Schema Object
abstract SchemaOb;
SchemaReference : SchemaOb ::= <Ref> <SchemaObject:SchemaObject>;
SchemaObject : SchemaOb ::= <AdditionalProperties:SchemaOb> <AdditionalPropertiesAllowed:Boolean> <DefaultValue:Object> <Description> <DeprecatedBoolean:Boolean> [DiscriminatorObject] EnumObj* <Example:Object> <ExclusiveMaximum:Boolean> <ExclusiveMinimum:Boolean> [ExternalDocObject] <Format> [ItemsSchema] <Maximum:Number> <Minimum:Number> <MaxItems:Integer> <MinItems:Integer> <MaxLength:Integer> <MinLength:Integer> <MaxProperties:Integer> <MinProperties:Integer> <MultipleOf:Number> [NotSchema] <Nullable:Boolean> <Pattern> PropertyItem* RequiredField* AllOfSchema* AnyOfSchema* OneOfSchema* <ReadOnly:Boolean> <WriteOnly:Boolean> <Type> <Title> <UniqueItems:Boolean> [XmlObject] Extension*;
AdditionalProperties ::= SchemaOb;
EnumObj ::= <EnumOb:Object>;
ItemsSchema ::= SchemaOb;
NotSchema ::= SchemaOb;
PropertyItem ::= <Name> SchemaOb;
RequiredField ::= <Value>;
AllOfSchema ::= SchemaOb;
AnyOfSchema ::= SchemaOb;
OneOfSchema ::= SchemaOb;

//Discriminator Object
DiscriminatorObject ::= <PropertyName> MappingTuple*;
MappingTuple ::= <Key> <Value>;

//XML Object
XmlObject ::= <Name> <Namespace> <Prefix> <Attribute:Boolean> <Wrapped:Boolean>;

//Security Scheme Object
abstract SecuritySchemeOb;
SecuritySchemeReference : SecuritySchemeOb ::= <Ref> <SecuritySchemeObject:SecuritySchemeObject>;
SecuritySchemeObject ::= <Type> <Description> <Name> <In> <Scheme> <BearerFormat> OAuthFlowsObject <OpenIdConnectUrl> Extension*;

//OAuth Flows Object
OAuthFlowsObject ::= [Implicit] [Password] [ClientCredentials] [AuthorizationCode];
Implicit ::= OAuthFlowObject;
Password ::= OAuthFlowObject;
ClientCredentials ::= OAuthFlowObject;
AuthorizationCode ::= OAuthFlowObject;

//OAuth Flow Object
OAuthFlowObject ::= <AuthorizationUrl> <TokenUrl> <RefreshUrl> ScopesTuple* <Configuration>;
ScopesTuple ::= <ScopesKey> <ScopesValue>;

//Security Requirement Object
SecurityRequirementObject ::= SecurityRequirementTuple*;
SecurityRequirementTuple ::= <Name> SecurityRequirementValue*;
SecurityRequirementValue ::= <Value>;