//OpenAPI Object
OpenAPIObject ::= <OpenAPI> [InfoObject] <JsonSchemaDialect> ServerObject* PathsObject* Webhook* [ComponentsObject] SecurityRequirementObject* TagObject* [ExternalDocObject] Extension* InferredParameter*;
InferredParameter ::= <Parameter>;
Webhook ::= <Key> PathItemOb;

//Info Object
InfoObject ::= <Title> <Summary> <Description> <TermsOfService> [ContactObject] [LicenseObject] <Version> Extension*;

//Contact Object
ContactObject ::= <Name> <Url> <Email> Extension*;

//License Object
LicenseObject ::= <Name> <Identifier> <Url> Extension*;

//Server Object
ServerObject ::= <Url> <Description> ServerVariablesTuple* Extension*;
ServerVariablesTuple ::= <Name> ServerVariableObject;

//Server Variable Object
ServerVariableObject ::= Enum* <Default> <Description> Extension*;
Enum ::= <EnumValue>;

//Components Object
ComponentsObject ::= SchemaTuple* ResponseTuple* ParameterTuple* ExampleTuple* RequestBodyTuple* HeaderTuple* SecuritySchemeTuple* LinkTuple* CallbackTuple* PathItemTuple* Extension*;
SchemaTuple ::= <Key> SchemaOb;
ResponseTuple ::= <Key> ResponseOb;
ParameterTuple ::= <Key> ParameterOb;
ExampleTuple ::= <Key> ExampleObject;
RequestBodyTuple ::= <Key> RequestBodyOb;
HeaderTuple ::= <Key> HeaderOb;
SecuritySchemeTuple ::= <Key> SecuritySchemeOb;
LinkTuple ::= <Key> LinkOb;
CallbackTuple ::= <Key> CallbackOb;
PathItemTuple ::= <Key> PathItemOb;

//Paths Object
PathsObject ::= <Ref> PathItemOb;

//Path Item Object
abstract PathItemOb;
PathItemReference : PathItemOb ::= <Ref> <Summary> <Description>;
PathItemObject : PathItemOb ::= <Ref> <Summary> <Description> [Get] [Put] [Post] [Delete] [Options] [Head] [Patch] [Trace] ServerObject* ParameterOb* Extension*;

//Operation Object
OperationObject ::= Tag* <Summary> <Description> [ExternalDocObject] <OperationID> ParameterOb* [RequestBodyOb] ResponsesObject CallbackTuple* <DeprecatedBoolean:Boolean> SecurityRequirementObject* ServerObject* Extension*;
Get ::= OperationObject;
Put ::= OperationObject;
Post ::= OperationObject;
Delete ::= OperationObject;
Options ::= OperationObject;
Head ::= OperationObject;
Patch ::= OperationObject;
Trace ::= OperationObject;
Tag ::= <Tag:String>;

//External Documentation Object
ExternalDocObject ::= <Description> <Url> Extension*;

//Parameter Object
abstract ParameterOb;
ParameterReference : ParameterOb ::= <Ref> <Summary> <Description>;
ParameterObject : ParameterOb ::= <Name> <In> <Description> <Required:Boolean> <DeprecatedBoolean:Boolean> <AllowEmptyValue:Boolean> <Style> <Explode:Boolean> <AllowReserved:Boolean> [SchemaOb] <Example:Object> ExampleTuple* ContentTuple* Extension*;
ContentTuple ::= <Key> MediaTypeObject;


//Request Body Object
abstract RequestBodyOb;
RequestBodyReference : RequestBodyOb ::= <Ref> <Summary> <Description>;
RequestBodyObject : RequestBodyOb ::= <Description> ContentTuple* <Required:Boolean> Extension*;

//Media Type Object
MediaTypeObject ::= [SchemaOb] <Example:Object> ExampleTuple* EncodingTuple* Extension*;
EncodingTuple ::= <Key> EncodingObject;

//Encoding Object
EncodingObject ::= <ContentType> HeaderTuple* <Style> <Explode:Boolean> <AllowReserved:Boolean> Extension*;

//Responses Object (ResponseTuple is used for HTTPStatusCode)
ResponsesObject ::= Default ResponseTuple*;
Default ::= ResponseOb;

//Response Object
abstract ResponseOb;
ResponseReference : ResponseOb ::= <Ref> <Summary> <Description>;
ResponseObject : ResponseOb ::= <Description> HeaderTuple* ContentTuple* LinkTuple* Extension*;

//Callback Object
abstract CallbackOb;
CallbackReference : CallbackOb ::= <Ref> <Summary> <Description>;
CallbackObject : CallbackOb ::= Expression* Extension*;
Expression ::= <Name> PathItemOb;
Extension ::= <Key> <Value:Object>;

//Example Object
ExampleObject ::= <Summary> <Description> <Value:Object> <ExternalValue> Extension*;

//Link Object
abstract LinkOb;
LinkReference : LinkOb ::= <Ref> <Summary> <Description>;
LinkObject : LinkOb ::= <OperationRef> <OperationID> LinkParameterTuple* <LinkRequestBody:Object> <Description> [ServerObject] Extension*;
LinkParameterTuple ::= <LinkParameterKey> <LinkParameterValue:Object>;

//Header Object
abstract HeaderOb;
HeaderReference : HeaderOb ::= <Ref> <Summary> <Description>;
HeaderObject : HeaderOb ::= <Description> <Required:Boolean> <DeprecatedBoolean:Boolean> <AllowEmptyValue:Boolean> <Style> <Explode:Boolean> <AllowReserved:Boolean> [SchemaOb] <Example:Object> ExampleTuple* ContentTuple* Extension*;

//Tag Object
TagObject ::= <Name> <Description> [ExternalDocObject] Extension*;

//Schema Object
abstract SchemaOb;
SchemaReference : SchemaOb ::= <Ref> <Summary> <Description>;
SchemaObject : SchemaOb ::= [AdditionalProperties] <AdditionalPropertiesAllowed:Boolean> <DefaultValue:Object> <Description> <DeprecatedBoolean:Boolean> [DiscriminatorObject] EnumObj* ExampleElement* <ExclusiveMaximum:Number> <ExclusiveMinimum:Number> [ExternalDocObject] <Format> [ItemsSchema] <Maximum:Number> <Minimum:Number> <MaxItems:Integer> <MinItems:Integer> <MaxLength:Integer> <MinLength:Integer> <MaxProperties:Integer> <MinProperties:Integer> <MultipleOf:Number> [NotSchema] <Pattern> PropertyItem* RequiredField* AllOfSchema* AnyOfSchema* OneOfSchema* <ReadOnly:Boolean> <WriteOnly:Boolean> <Type> TypeArray* <Title> <UniqueItems:Boolean> <MaxContains:Integer> <MinContains:Integer> <DependentRequired:Object> <DependentSchema:SchemaOb> <Const:Object> [XmlObject] Extension*;
AdditionalProperties ::= SchemaOb;
EnumObj ::= <EnumOb:Object>;
ItemsSchema ::= SchemaOb;
NotSchema ::= SchemaOb;
PropertyItem ::= <Name> SchemaOb;
RequiredField ::= <Value>;
AllOfSchema ::= SchemaOb;
AnyOfSchema ::= SchemaOb;
OneOfSchema ::= SchemaOb;
ExampleElement ::= <Example:Object>;
TypeArray ::= <TypeElements:Object>;


//Discriminator Object
DiscriminatorObject ::= <PropertyName> MappingTuple* Extension*;
MappingTuple ::= <Key> <Value>;

//XML Object
XmlObject ::= <Name> <Namespace> <Prefix> <Attribute:Boolean> <Wrapped:Boolean> Extension*;

//Security Scheme Object
abstract SecuritySchemeOb;
SecuritySchemeReference : SecuritySchemeOb ::= <Ref> <Summary> <Description>;
SecuritySchemeObject : SecuritySchemeOb ::= <Type> <Description> <Name> <In> <Scheme> <BearerFormat> [OAuthFlowsObject] <OpenIdConnectUrl> Extension*;

//OAuth Flows Object
OAuthFlowsObject ::= [Implicit] [Password] [ClientCredentials] [AuthorizationCode] Extension*;
Implicit ::= OAuthFlowObject;
Password ::= OAuthFlowObject;
ClientCredentials ::= OAuthFlowObject;
AuthorizationCode ::= OAuthFlowObject;

//OAuth Flow Object
OAuthFlowObject ::= <AuthorizationUrl> <TokenUrl> <RefreshUrl> ScopesTuple* Extension*;
ScopesTuple ::= <ScopesKey> <ScopesValue>;

//Security Requirement Object
SecurityRequirementObject ::= SecurityRequirementTuple*;
SecurityRequirementTuple ::= <Name> SecurityRequirementValue*;
SecurityRequirementValue ::= <Value>;